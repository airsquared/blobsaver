/*
 * Copyright (c) 2020  airsquared
 *
 * This file is part of blobsaver.
 *
 * blobsaver is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 of the License.
 *
 * blobsaver is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with blobsaver.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'application'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.beryx.jlink' version '2.23.3'

    id 'idea'
}
idea.module.outputDir file("out/production/classes") // fix running via IntelliJ

/*
 * REMEMBER: also update the version string in:
 *  - Main.java
 */
version = "2.6"
description = "A cross-platform GUI app for saving SHSH blobs"
String appIdentifier = "airsquared.blobsaver.app"
String copyright = "Copyright (c) 2020 airsquared"
def os = DefaultNativePlatform.currentOperatingSystem

startScripts.enabled = distZip.enabled = distTar.enabled = false

repositories {
    mavenCentral()
}

dependencies {
    String fxPlatform = ""
    if (os.isMacOsX()) fxPlatform = 'mac'
    else if (os.isWindows()) fxPlatform = 'win'
    else if (os.isLinux()) fxPlatform = 'linux'

    implementation "org.openjfx:javafx-base:15.0.1:$fxPlatform"
    implementation "org.openjfx:javafx-controls:15.0.1:$fxPlatform"
    implementation "org.openjfx:javafx-graphics:15.0.1:$fxPlatform"
    implementation "org.openjfx:javafx-fxml:15.0.1:$fxPlatform"

    implementation group: 'org.json', name: 'json', version: '20210307'
    implementation 'de.jangassen:nsmenufx:3.1.0'
//    implementation 'net.java.dev.jna:jna:jpms:5.7.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(15)
    }
}

compileJava {
    options.compilerArgs = ['--module-path', classpath.asPath]
}

application {
    mainModule = "airsquared.blobsaver"
    mainClass = "airsquared.blobsaver.app.Main"
}

private String getJarDirectory() {
    String dir = ""
    if (DefaultNativePlatform.currentOperatingSystem.isMacOsX()) dir = "macos/Contents"
    else if (DefaultNativePlatform.currentOperatingSystem.isWindows()) dir = "windows/files"
    else if (DefaultNativePlatform.currentOperatingSystem.isLinux()) dir = "linux"
    return "${projectDir}/dist/${dir}";
}

run {
    systemProperty "jar.directory", getJarDirectory()
}

test {
    useJUnitPlatform()
    systemProperty "jar.directory", getJarDirectory()
}

jlink {
    addOptions '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages', '--strip-native-commands',
            '--dedup-legal-notices=error-if-not-same-content'
    launcher {
        jvmArgs = [ '-Djar.directory={{BIN_DIR}}' ]
        noConsole = true
    }
    jpackage {
        imageOptions = [ '--copyright', copyright, '--description', description, '--vendor', 'airsquared']

        if (os.isMacOsX()) {
            installerType = "dmg"
            imageOptions.addAll '--mac-package-identifier', appIdentifier
            icon = "${projectDir}/dist/macos/Contents/Resources/blob.icns"
        } else if (os.isWindows()) {
            installerOptions.addAll '--win-dir-chooser', '--win-menu', '--win-shortcut'
            icon = "${projectDir}/dist/windows/blob.ico"
        } else {
            installerOptions.addAll '--linux-shortcut', '--linux-menu-group', 'Utility;Archiving;Java',
                    '--linux-rpm-license-type', 'GPLv3'
            icon = "${projectDir}/src/main/resources/airsquared/blobsaver/app/blob_emoji.png"
        }
    }
    if (os.isMacOsX()) jpackageImage.doLast {
        copy {
            from "${projectDir}/dist/macos/Contents/MacOS" into "${buildDir}/jpackage/blobsaver.app/Contents/MacOS"
        }
        copy {
            from "${projectDir}/dist/macos/Contents/Frameworks" into "${buildDir}/jpackage/blobsaver.app/Contents/Frameworks"
        }
        copy {
            from "${projectDir}/LICENSE", "${projectDir}/libraries_used.txt"
            into "${buildDir}/jpackage/blobsaver.app/Contents/Resources"
        }
    } else if (os.isWindows()) jpackageImage.doLast {
        copy {
            from "${projectDir}/dist/windows/files", "${buildDir}/jpackage/blobsaver/runtime/bin/zip.dll"
            rename "LICENSE_windows", "LICENSE"
            rename "libraries_used_windows.txt", "libraries_used.txt"
            into "${buildDir}/jpackage/blobsaver"
        }
    } else jpackageImage.doLast {
        copy {
            from "${projectDir}/dist/linux", "${projectDir}/LICENSE", "${projectDir}/libraries_used.txt"
            into "${buildDir}/jpackage/blobsaver/lib"
        }
    }

}

task createLinuxTargz(type: Tar, dependsOn: jpackageImage) {
    archiveFileName = "blobsaver-linux.tar.gz"
    compression = Compression.GZIP
    destinationDirectory = file("${buildDir}/distributions/")
    from "${buildDir}/jpackage/blobsaver"

    if (os.isLinux()) {
        assemble.dependsOn createLinuxTargz
    }
}

task windowsInstaller(dependsOn: jpackageImage) { // requires inno setup to be installed
    doFirst {
        copy {
            from "${projectDir}/dist/windows/blobsaver.iss" into "${buildDir}/jpackage"
            filter(ReplaceTokens, tokens: [AppName: project.name, AppVersion: version, AppCopyright: copyright])
        }
        exec {
            commandLine "iscc", "/Qp", "${buildDir}\\jpackage\\blobsaver.iss"
        }
        delete "${buildDir}/jpackage/blobsaver.iss"
    }
}

assemble.dependsOn os.isWindows() ? windowsInstaller : jpackage