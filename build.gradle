/*
 * Copyright (c) 2019  airsquared
 *
 * This file is part of blobsaver.
 *
 * blobsaver is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, version 3 of the License.
 *
 * blobsaver is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with blobsaver.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'edu.sc.seis.launch4j' version '2.4.4' // for bundling into .exe
    id "edu.sc.seis.macAppBundle" version "2.3.0" // for bundling into .app
    id 'com.github.johnrengelman.shadow' version '4.0.4' // for fat jar
    id 'se.patrikerdes.use-latest-versions' version '0.2.7' // for updating Gradle plugins
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'java'
}

version null
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://repo.spring.io/plugins-release/" // for junique
    }
}

dependencies {
    compile group: 'org.json', name: 'json', version: '20180813'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile group: 'it.sauronsoftware', name: 'junique', version: '1.0.4' //for only one copy of blobsaver at a time
    compile "de.codecentric.centerdevice:centerdevice-nsmenufx:2.1.6" //for macOS menu bar
//    compile 'com.google.code.gson:gson:2.8.5' // for app preferences TODO: implement this
    compile 'net.java.dev.jna:jna:5.2.0' //for dock visibility
}

build {
//    minifyEnabled = true
//    buildConfigField "boolean", "debugMode", false //maybe only works for android?
}

compileJava {
    options.fork = true
    options.forkOptions.javaHome = new File("${System.properties.'java.home'}")
    options.compilerArgs << "-XDignore.symbol.file=true"
}

jar {
    manifest {
        attributes 'Main-Class': 'com.airsquared.blobsaver.Main'
    }
}

shadowJar {
    classifier = null // remove '-all' suffix
    from "LICENSE"
}

macAppBundle {
    mainClassName = "com.airsquared.blobsaver.Main"
//    bundleJRE = true
    icon = "icons/blob_emoji.icns"
    backgroundImage = "icons/dmg_background.png"
    appName = 'blobsaver'
    dmgName = 'blobsaver-macos'
    volumeName = 'blobsaver-macos'
}

launch4j { // Windows only, don't use this, use createWindowsInstaller
    mainClassName = "com.airsquared.blobsaver.Main"
    copyright = "Copyright (c) 2019  airsquared"
    downloadUrl = "https://java.com/inc/BrowserRedirect1.jsp"
    icon = "${projectDir}/icons/blob_emoji.ico"
}

//you need to be on Windows or have 'wine' in your CLASSPATH
task createWindowsInstaller(type: Exec, dependsOn: 'createExe') {
    // IMPORTANT: first make sure you have installed Inno Setup
    //            Install Inno Setup Preprocessor (ISPP) along with Inno Setup
    //            and make sure you have wine in your path if you are not on windows
    //            Inno Setup **MUST** be installed to C:\Program Files (x86)\Inno Setup 5\
    doFirst {
        new File(projectDir, "build/innosetup/").mkdirs()
        workingDir = projectDir
        println "running Inno Setup Compiler"
        if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")) {
            commandLine 'C:\\Program Files (x86)\\Inno Setup 5\\ISCC.exe', '/Qp', 'blobsaver.iss'
        } else {
            commandLine 'wine', 'C:\\Program Files (x86)\\Inno Setup 5\\ISCC.exe', '/Qp', 'blobsaver.iss'
        }
    }
}

task buildEverything {
    doLast {
        buildEverythingFunc()
    }
}

void buildEverythingFunc() {
    println "creating fat/uber jar with shadowJar:"
    shadowJar.execute()
    println "creating mac app bundle:"
    if (System.getProperty("os.name").toUpperCase().contains("MAC")) {
        println "  creating full .dmg"
        createDmg.execute()
    } else {
        println "  creating .zip with .app inside"
        createAppZip.execute()
    }
    println "creating windows installer with inno setup:"
    println "         **IMPORTANT:** (for creating windows installer)"
    println "  first make sure you have installed Inno Setup first"
    println "  and make sure you have wine in your path if you are not on windows"
    println "  Inno Setup **MUST** be installed to C:\\Program Files (x86)\\Inno Setup 5\\"
    createWindowsInstaller.execute()
}